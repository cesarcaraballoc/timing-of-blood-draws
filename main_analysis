/*

Timing of blood draws among hospitalized patients: An evaluation of the electronic health records from a large healthcare system 

Main analysis

*/

u processed,clear

**# Table and timing distribution

tabstat timing, s(p25 p50 p75 mean sd) 
tabstat timing, s(p25 p50 p75 mean sd) by(race)
tabstat timing, s(p25 p50 p75 mean sd) by(gender)
tabstat timing, s(p25 p50 p75 mean sd) by(agecat)

kwallis timing, by(race)
kwallis timing, by(agecat)
kwallis timing if gender!="Unknown", by(gender)

tab four_eight race, chi2 col
tab four_eight agecat, chi2 col
tab four_eight gender if gender!="Unknown", chi2 col

**# hourly % of those between 4 and 8

tab four_eight
tab four_five
tab five_six
tab six_seven
tab seven_eight

tab four_five if four_eight==1
tab five_six if four_eight==1
tab six_seven if four_eight==1
tab seven_eight if four_eight==1

**# % at the beginning and end of the study  

foreach K in four_eight four_five five_six six_seven seven_eight {
disp "`K' in Nov 2016"
tabstat `K' if monthly==682, s(mean sd sem)

disp "`K' in Oct 2019"
tabstat `K' if monthly==717, s(mean sd sem)
}

**# regression: rate of change

*** trends and change. Denominator is all samples.

u processed,clear

#delimit ;

collapse  	(mean) four_eight (semean) four_eight_se=four_eight 
			(mean) four_five (semean) four_five_se=four_five
			(mean) five_six (semean) five_six_se=five_six
			(mean) six_seven (semean) six_seven_se=six_seven
			(mean) seven_eight (semean) seven_eight_se=seven_eight
			
			, by(monthly)
			;
#delimit cr

** trends, ARIMA

tsset monthly


foreach K in four_eight four_five five_six six_seven seven_eight {
arima `K' monthly, ar(1) nolog 

}


** change 

keep if inlist(monthly,682,717)
sort monthly


foreach K in four_eight four_five five_six six_seven seven_eight {

local zcrit=invnorm(.975)

noi disp " ******** diff in `K'"
    local diff=`K'[2]-`K'[1]         
    local SE = sqrt(`K'_se[2]^2+`K'_se[1]^2)
    local lb=`diff'-`zcrit'*`SE'
    local ub=`diff'+`zcrit'*`SE'
    local Pval=(2*(1-normal(abs(`diff'/`SE'))))
    local Pval=cond(`Pval'<0.001,"<0.001",string(`Pval',"%5.3f"))
    noi di "`R'" _col(20) %5.2f `diff'*100 _col(30) "(" %5.2f `lb'*100 "," %5.2f `ub'*100 ")" _col(50) "`Pval'"
}

**# histogram

u processed,clear

histogram timing, xlabel( 0 "00:00" 2 "02:00" 4 "04:00" 6 "06:00" 8 "08:00" 10 "10:00" 12 "12:00" 14 "14:00" 16 "16:00" 18 "18:00" 20 "20:00" 22 "22:00") ylabel(0(2)20) percent bin(24) xmtick(##2)  xtitle("Time of blood draw") addlabel addlabopts(mlabangle(h) yvarformat(%2.1f)) 
graph save timing_distribution.gph,replace

**# Figure trends

u processed,clear
collapse (mean) four_eight (mean) three_four (mean) four_five (mean) five_six (mean) six_seven (mean) seven_eight, by(monthly)

#delimit ;
mylabels 0(5)60, myscale(@/100) local(myla);
twoway 
	(connected four_eight monthly) 
	(connected four_five monthly) 
	(connected five_six monthly) 
	(connected six_seven monthly) 
	(connected seven_eight monthly) 
	,
	xlabel(682 "Nov 2016" 688 "May 2017" 694 "Nov 2017" 700 "May 2018" 706 "Nov 2018" 712 "May 2019" 717 "Oct 2019") 
	ylabel(`myla') 
	xtitle("") 
	ytitle("Percent of total blood draws") 
	legend(order(1 "4:00 to 7:59" 2 "4:00 to 4:59" 3 "5:00 to 5:59" 4 "6:00 to 6:59" 5 "7:00 to 7:59") title("Timing"))
	;
	
graph save figure_trends.gph,replace ;

#delimit cr

* only 4-8

u processed, clear

collapse (mean) four_five (mean) five_six (mean) six_seven (mean) seven_eight, by(monthly)

#delimit ;
mylabels 0(2)20, myscale(@/100) local(myla);
twoway  
	(connected four_five monthly) 
	(connected five_six monthly) 
	(connected six_seven monthly) 
	(connected seven_eight monthly) 
	,
	xlabel(682 "Nov 2016" 688 "May 2017" 694 "Nov 2017" 700 "May 2018" 706 "Nov 2018" 712 "May 2019" 717 "Oct 2019") 
	ylabel(`myla') 
	xtitle("") 
	ytitle("Percent of total blood draws") 
	legend(order(1 "4:00 to 4:59" 2 "5:00 to 5:59" 3 "6:00 to 6:59" 4 "7:00 to 7:59") title("Timing"))
	;
	
graph save figure_trends_four_eight.gph,replace ;

#delimit cr
